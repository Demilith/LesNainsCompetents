apply plugin: 'application'
apply plugin: 'java'

def MAIN_TITLE = 'LibraryProject'
def MAIN_VERSION = '1.0'
mainClassName = 'com.cesi.library_project.Main'

sourceSets {
    main {
        java { srcDir "src" }
        resources { srcDir "resources" }
    }
    linux {
        java { srcDir "src" }
        resources { srcDir "resources" }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
    macos {
        java { srcDir "src" }
        resources { srcDir "resources" }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
    windows {
        java { srcDir "src" }
        resources { srcDir "resources" }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

configurations {
    linuxCompile { extendsFrom compile }
    macosCompile { extendsFrom compile }
    windowsCompile { extendsFrom compile }
}

dependencies {

    //use of the available SWT jar but to only make the IDE validating the classes
    compileOnly fileTree(dir: 'libs', include: 'macos_swt.jar')

    //specific swt version according to the OS
    macosCompile fileTree(dir: 'libs', include: 'macos_swt.jar')
    linuxCompile fileTree(dir: 'libs', include: 'linux_swt.jar')
    windowsCompile fileTree(dir: 'libs', include: 'windows_swt.jar')

    //indication de récupérer un fichier spécifique durant la phrase de compilation
    compile files('libs/sqlite-jdbc-3.21.0.jar')
    compile files('libs/SQLiteDatabaseModel-Core-1.0.jar')
    compile files('libs/SQLiteDatabaseModel-Jdbc-1.0.jar')
    compile 'org.checkerframework:checker:2.1.12'
}

def sources = [
        sourceSets.linux,
        sourceSets.macos,
        sourceSets.windows
]

def compiles = [
        configurations.linuxCompile,
        configurations.macosCompile,
        configurations.windowsCompile
]

["Linux", "Macos", "Windows"].eachWithIndex { name, number ->
    task "execute${name}" (type:JavaExec) {
        jvmArgs "-XstartOnFirstThread"
        main = mainClassName
        classpath = sources[number].runtimeClasspath
    }

    task "jar${name}" (type: Jar) {
        baseName = "${name}_${MAIN_TITLE}"
        version = MAIN_VERSION

        manifest {
            attributes 'Implementation-Title': MAIN_TITLE,
                    'Implementation-Version': MAIN_VERSION,
                    'Main-Class': mainClassName
        }

        from { compiles[number].collect { it.isDirectory() ? it : zipTree(it) } }
        from sources[number].output
    }
}